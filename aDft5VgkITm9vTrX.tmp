##C:\Users\marti\OneDrive\Documents\Vic20\BetterBeeReady\main.bas
10 rem Better Bee Ready
11 rem Save memory for icons
12 poke 52,28: poke 56,28: clr
13 gosub 200 
15 dim sd(7)
18 dim cd(7)
20 let hv = 7680
30 let bh = 3
40 let fc = 4
50 let fi = 2
60 let hc = 2 : let bc=7
70 let ch = 38400
80 let sz=505
90 let bp = 7681 : let lp=bp
100 let bi=0
117 rem Compass Directions
118 gosub 240 : gosub 540
120 print chr$(147)
125 rem Draw Hive
130 gosub 1000
131 rem Draw Flowers
140 gosub 1050
143 rem Calculate position of bee
145 gosub 310
146 rem Bee Sniff
147 gosub 360
148 rem Draw Bee
149 gosub 600
150 get a$: if a$="" then goto 143
160 print chr$(147)
165 poke 36869,240
170 end
200 rem copy the chr set
210 for i=7168 TO 7679: poke i,peek(i+25600): next
230 return
240 rem set up the characters
250 for i=0 to 31:read z:poke 7168+i,z:next:poke 36869,255
252 return
260 data 8,28,28,99,127,34,62,8
265 data 195,255,90,126,102,90,60,0
270 data 8,28,42,119,42,8,8,28
275 data 0,24,60,102,66,126,66,90
##C:\Users\marti\OneDrive\Documents\Vic20\BetterBeeReady\beemovie.bas
300 rem Moving the bee.
310 rem Calculate the position of the bee
320 if bp<hv or bp>hv+sz then print "error": return
330 let rw = int((bp-hv)/22)
340 let cl = bp-(hv+(rw*22))
350 return
360 rem Bee sniff
370 for i=0 to 7
380 let sd(i)=0
390 if rw=0 and cl=0 and i=2 then sd(i)=cd(i)
400 if rw=0 and (cl>0 and cl<21) and (i>=2 and i<=6) then sd(i)=cd(i)
410 if (rw=0 and cl=21) and (i>=4 and i<=6) then sd(i)=cd(i)
420 if (rw>0 and rw<22) and cl=0 and (i>=0 and i<=4) then sd(i)=cd(i)
430 if (rw>0 and rw<22) and cl=21 and (i=0 or (i>=4 and i<=7)) then sd(i)=cd(i)
440 if (rw=22 and cl=0) and (i>=0 and i<=2) then sd(i)=cd(i)
450 if(rw=22 and(cl>0 and cl<21))and((i>=0 or i<=2)or(i=6 or i=7))thensd(i)=cd(i)
460 if (rw=22 and cl=21) and (i=0 or i=6 or i=7) then sd(i)=cd(i)
470 if (rw>0 and rw<22) and (cl>0 and cl<21) then sd(i)=cd(i)
480 next i
490 for i=0 to 7
500 if sd(i)<>0 then let pv=peek(bp+sd(i))
510 if pv=fi then let bp = bp+sd(i)
520 next i
522 if bp<>lp then return
524 rem no flower so random move
525 if bp=lp then rd=int(rnd(0)*7)+1 : if sd(rd)=0 then goto 525
530 let bp=bp+sd(rd):return
540 rem Load compass directions
550 for i=0 to 7:read cd(i):next
580 data -22,-21,1,23,22,21,-1,-23
590 return
600 rem Draw Bee.
605 if bp=hv then goto 630
610 poke lp,32
620 poke bp,bi
625 poke ch+(bp-hv),bc
630 let lp=bp
640 return
##C:\Users\marti\OneDrive\Documents\Vic20\BetterBeeReady\drawworld.bas
1000 rem Draw the hive
1010 if hv=0 or ch=0 or bh=0 or hc=0 then print "error" : return
1020 poke hv,bh
1030 poke ch,hc
1040 return
1050 rem Draw the flowers
1060 if hv=0 or ch=0 or bh=0 or hc=0 then print "error" : return
1070 let nf = int(rnd(0)*10)+1
1080 if nf < 5 then goto 1070
1090 for ro = 1 to nf
1100 let fp = int(rnd(0)*sz)+1
1110 poke hv+fp,fi
1120 poke ch+fp,fc
1130 next ro
1135 poke hv+1,bi
1138 poke ch+1,bc
1140 return
